#!/bin/awk -f
# aur-arrow - print package/dependency pairs

BEGIN {
    FS="[<=>]"
}

/pkgbase/ {
    in_split_pkg = 0
    pkgbase = $2

    # track both the pkgbases themselves and their number of deps
    dep_counts[pkgbase] = 0 
}

/^\t(make|check)?depends/ {
    if (!length(pkgbase)) {
        print "pkgbase unavailable" > "/dev/stderr"
        exit 1
    }
    if (!in_split_pkg) {
        # POSIX array of arrays!
        pkg_deps[pkgbase,++dep_counts[pkgbase]] = $2
    }
}

/^$/ {
    in_split_pkg = 1
}

/pkgname/ || /provides/ {
    if (!length(pkgbase)) {
        print "pkgbase unavailable" > "/dev/stderr"
        exit 1
    }
    pkg_map[$2] = pkgbase
}

END {
    for (pkgbase in dep_counts) {
      for (dep = 1; dep <= dep_counts[pkgbase]; dep++) {
            dep_pkgname = pkg_deps[pkgbase,dep]
            # only print dependency if it was encountered before
            if (dep_pkgname in pkg_map)
                printf("%s\t%s\n", pkgbase, pkg_map[dep_pkgname])
        }
    }
}

