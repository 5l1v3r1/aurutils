#!/bin/gawk -f

BEGIN {
    FS="[<=>]"
}

/pkgbase/ {
    dep_count = 0
    in_split_pkg = 0
    pkgbase = $2
}

/^\t(make|check)?depends/ {
    if (!length(pkgbase)) {
        print "pkgbase unavailable" > "/dev/stderr"
        exit 1
    }
    if (!in_split_pkg) {
        # note: gawk array of arrays
        pkg_deps[pkgbase][dep_count++] = $2
    }
}

/^$/ {
    in_split_pkg = 1
}

/pkgname/ {
    if (!length(pkgbase)) {
        print "pkgbase unavailable" > "/dev/stderr"
        exit 1
    }
    pkg_map[$2] = pkgbase
}

END {
    for (pkgbase in pkg_deps) {
        for (dep in pkg_deps[pkgbase]) {
            dep_pkgname = pkg_deps[pkgbase][dep]
            # only print dependency if it was encountered before
            if (dep_pkgname in pkg_map)
                printf("%s\t%s\n", pkgbase, pkg_map[dep_pkgname])
        }
    }
}
